import org.jetbrains.annotations.Nullable

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'androidx.navigation.safeargs'

apply plugin: 'com.google.gms.google-services'

apply plugin: 'io.fabric'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.mucahitkambur.tdksozluk"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 4
        versionName "1.0.5"
    }
    dataBinding {
        enabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        release {
            Properties properties = getProperties('../keystore.properties')
            storeFile file(getProperty(properties, 'store'))
            keyAlias = getProperty(properties, 'alias')
            storePassword getProperty(properties, 'storePass')
            keyPassword getProperty(properties, 'pass')
        }
    }

    buildTypes {
        debug {
            ext.enableCrashytics = false
            signingConfig signingConfigs.debug
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "version"
    productFlavors {
        dev {
            dimension "version"

            applicationIdSuffix ".develop"
            resValue "string", "app_name", "TDK Sözlük Develop"
            ext.alwaysUpdateBuildId = false //for io.fabric
            ext.crunchPngs = false

            buildConfigField "String", "END_POINT", "\"${end_point}\""
            buildConfigField "String", "GITHUB_URL", "\"${github_url}\""
            buildConfigField "String", "MAIL_ADDRESS", "\"${mail_address}\""
            buildConfigField "String", "ALPHABET_IMG_URL", "\"${alphabet_image_url}\""
        }

        production {
            dimension "version"

            resValue "string", "app_name", "TDK Sözlük"
            buildConfigField "String", "END_POINT", "\"${end_point}\""
            buildConfigField "String", "GITHUB_URL", "\"${github_url}\""
            buildConfigField "String", "MAIL_ADDRESS", "\"${mail_address}\""
            buildConfigField "String", "ALPHABET_IMG_URL", "\"${alphabet_image_url}\""
        }
    }
}

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

@Nullable
def getProperties(String fileName){
    final Properties properties = new Properties()
    def file = file(fileName)
    if(file.exists()){
        file.withInputStream { stream -> properties.load(stream)}
    }
    return properties
}

static def getProperty(@Nullable Properties properties, String name){
    return properties.getProperty(name) ?: "$name missing"
}

dependencies {
    def nav_version = "2.2.0-alpha01"
    def dagger_version = "2.16"
    def retrofit_version = "2.6.0"
    def room_version = "2.1.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.core:core:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'

    //Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.3'

    //Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    //Lifecycle
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-alpha03'

    //Binding
    kapt "com.android.databinding:compiler:3.1.4"

    //Firebase
    implementation 'com.google.firebase:firebase-analytics:17.2.0'
    implementation 'com.google.firebase:firebase-core:17.2.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.google.android.gms:play-services-ads:18.2.0'

    //3th Party
    implementation 'com.github.Ferfalk:SimpleSearchView:0.1.4'
    implementation 'com.tapadoo.android:alerter:4.0.2'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'
    implementation 'com.github.captain-miao:optroundcardview:1.0.0'
}
